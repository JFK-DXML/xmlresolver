buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:4.1.0"
    classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8'
  }
}

plugins {
  id "java"
  id "maven"
  id 'de.fuerstenau.buildconfig' version '1.1.8'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "biz.aQute.bnd.builder"
apply plugin: 'idea'

import aQute.bnd.gradle.Bundle

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://maven.restlet.com" }
}

dependencies {
  compile (
    [group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'],
    [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25' ]
  )
  testImplementation (
    [group: 'junit', name: 'junit', version: '4.12'],
    [group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.1'],
    [group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'],
    [group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1']
  )
}

buildConfig {
    appName = "org.xmlresolver"
    version = project.version
    clsName = 'BuildConfig'
    packageName = "org.xmlresolver"
    charset = 'UTF-8'
}

// Avoid warnings about annoation processors and, optionally, the -source 1.7
// classpath. To avoid the class path warning, set JDK7LIB as described in
// gradle.properties
compileJava {
    options.compilerArgs += ['-proc:none', '-Xlint:deprecation']
}

compileTestJava {
    options.compilerArgs += '-proc:none'
}

jar {
    baseName "xmlresolver"
    version version
}

task javadocJar(type: Jar) {
    baseName "xmlresolver"
    version version
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    baseName "xmlresolver"
    version version
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

def mavenPom = {
  name 'XML Resolver'
  packaging 'jar'
  description 'An XML entity/uri resolver'
  url 'https://github.com/ndw/xmlresolver'

  scm {
       url 'scm:git@github.com:ndw/xmlresolver.git'
       connection 'scm:git@github.com:ndw/xmlresolver.git'
       developerConnection 'scm:git@github.com:ndw/xmlresolver.git'
   }

   licenses {
       license {
           name 'The Apache Software License, Version 2.0'
           url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
           distribution 'repo'
       }
   }

   developers {
       developer {
           id 'ndw'
           name 'Norman Walsh'
       }
   }
}

install {
  repositories {
    mavenInstaller {
      pom.project(mavenPom)
    }
  }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project(mavenPom)
        }
    }
}
