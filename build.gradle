import groovy.swing.SwingBuilder
import java.awt.Point

buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id "java"
  id "maven"
  id "signing"
}

repositories {
  mavenCentral()
  maven { url "http://maven.restlet.org" }
}

dependencies {
  compile (
    [group: 'junit', name: 'junit', version: '4.12'],
    [group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.2.5'],
    [group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'],
    [group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1'],
    [group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.1'],
    [group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.10'],
    [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.10'],
  )
}

jar {
  archiveName "xmlresolver-" + version + ".jar"
}

// I refuse to put my PGP password in a file.
//
// Adapted from
// https://www.timroes.de/2014/01/19/using-password-prompts-with-gradle-build-files/
//
// N.B. This can't work in a CI environment so there's no way to
// automatically publish the artifacts from, for example, Travis CI.
//
gradle.taskGraph.whenReady { taskGraph ->
  if(taskGraph.hasTask(':uploadArchives')) {

    def pass = ''
    if(System.console() == null) {
      new SwingBuilder().edt {
        dialog(modal: true,
            title: 'Enter password',
            alwaysOnTop: true,
            resizable: false,
            locationRelativeTo: null,
            locationByPlatform: true,
            // On my Linux multi-monitor setup, the dialog "appears" at
            // the top of the virtual desktop below my desktop. So move
            // the damned thing somewhere visible. Meh.
            location: new Point(100,100),
            pack: true,
            show: true
        ) {
          vbox { //
            label(text: "Please enter key passphrase:")
            input = passwordField()
            button(defaultButton: true, text: 'OK', actionPerformed: {
              pass = input.password;
              dispose();
            })
          }
        }
      }
    } else {
      pass = System.console().readPassword("\nPlease enter key passphrase: ")
      pass = new String(pass)
    }

    if (pass.size() <= 0) {
      throw new InvalidUserDataException("You must enter a password to proceed.")
    }

    allprojects { ext."signing.password" = pass }

  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task copyJar(dependsOn: jar, type: Copy) {
  from "build/libs/xmlresolver-" + version + ".jar"
  into "build/dist"
}

task copyJavaDoc(dependsOn: javadoc, type: Copy) {
  from "build/docs"
  into "build/dist/docs"
}

task copyLicense(dependsOn: copyJavaDoc, type: Copy) {
  from "LICENSE.md"
  into "build/dist/docs"
}

task copyReadme(dependsOn: copyJavaDoc, type: Copy) {
  from "README.md"
  into "build/dist/docs"
}

task copyDocs(dependsOn: [copyLicense, copyReadme]) {
  // nop
}

task makeDist(dependsOn: [ build, copyJar, copyDocs ]) {
  doLast {
    println "Created distribution in build/dist"
  }
}

task zipDist(dependsOn: makeDist, type: Zip) {
  from('build/dist')
  into 'xmlresolver-' + version
  archiveName 'xmlresolver-' + version + ".zip"
}

task dist(dependsOn: zipDist) {
  // nop
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'XML Resolver'
               packaging 'jar'
               description 'An XML entity/uri resolver'
               url 'https://github.com/ndw/xmlresolver'

               scm {
                   url 'scm:git@github.com:ndw/xmlresolver.git'
                   connection 'scm:git@github.com:ndw/xmlresolver.git'
                   developerConnection 'scm:git@github.com:ndw/xmlresolver.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'ndw'
                       name 'Norman Walsh'
                   }
               }
           }
        }
    }
}
